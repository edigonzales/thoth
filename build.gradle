plugins {
    id 'java'
    id 'application'
}

group = 'guru.interlis'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'guru.interlis.thoth.ThothCli'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'info.picocli:picocli:4.7.7'
    implementation 'org.freemarker:freemarker:2.3.34'
    implementation 'org.asciidoctor:asciidoctorj:3.0.1'
    implementation 'org.jsoup:jsoup:1.22.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'guru.interlis.thoth.ThothCli'
    }
}

tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Assembles an executable fat jar.'
    archiveBaseName = 'thoth'
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'guru.interlis.thoth.ThothCli'
    }

    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('.jar') }.collect { zipTree(it) }
    }
}

build {
    dependsOn tasks.named('fatJar')
}
